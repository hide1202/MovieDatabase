apply plugin: 'com.android.application'
apply plugin: 'dagger.hilt.android.plugin'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply from: '../dependencies.gradle'
apply from: '../external.gradle'

android {
    compileSdkVersion versions.android.compileSdk

    defaultConfig {
        applicationId "io.viewpoint.moviedatabase"
        minSdkVersion versions.android.minSdk
        targetSdkVersion versions.android.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            buildConfigField("String", "API_KEY", "\"${external.apiKey}\"")
        }
        release {
            buildConfigField("String", "API_KEY", "\"${external.apiKey}\"")

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles fileTree(dir: 'proguard', include: ['*.pro']).asList().toArray()
        }
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

kapt {
    correctErrorTypes true // for hilt
}

dependencies {
    implementation project(':model')
    implementation project(':api')
    implementation project(':domain')

    implementation deps.kotlin.stdlib

    // androidx
    implementation deps.androidx.core
    implementation deps.androidx.activity
    implementation deps.androidx.fragment
    implementation deps.androidx.appcompat
    implementation deps.androidx.material
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.lifecycle_viewmodel
    implementation deps.androidx.lifecycle_livedata
    implementation deps.androidx.lifecycle_runtime
    implementation deps.androidx.startup
    implementation deps.androidx.paging

    // dagger/hilt
    implementation deps.libraries.hilt
    kapt deps.libraries.hilt_kapt
    // hilt with androidx
    implementation deps.androidx.hilt_lifecycle
    kapt deps.androidx.hilt_kapt

    // timber
    implementation deps.libraries.timber

    // glide
    implementation deps.libraries.glide
    kapt deps.libraries.glide_kapt

    // test
    testImplementation deps.test.junit
    testImplementation deps.libraries.moshi

    // android test
    androidTestImplementation deps.android_test.junit
    androidTestImplementation deps.android_test.espresso_core
}