apply plugin: 'com.android.application'
apply plugin: 'dagger.hilt.android.plugin'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

apply from: '../dependencies.gradle'
apply from: '../external.gradle'
apply from: '../app.gradle'
apply from: '../android-configure.gradle'
apply from: '../android-test-dependencies.gradle'
apply from: '../android-jacoco.gradle'

android {
    defaultConfig {
        applicationId "io.viewpoint.moviedatabase"
    }

    signingConfigs {
        releaseKeystore {
            Properties prop = new Properties()
            prop.load(project.rootProject.file('keystore.properties').newDataInputStream())
            storeFile project.rootProject.file('debug.keystore')
            storePassword prop.getProperty('storePassword')
            keyAlias prop.getProperty('keyAlias')
            keyPassword prop.getProperty('keyPassword')
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled((findProperty("enableJacoco") ?: "false").toBoolean())
            buildConfigField("String", "API_KEY", "\"${external.apiKey}\"")
            buildConfigField("int", "DATABASE_VERSION", "${app.database_version}")
        }
        release {
            buildConfigField("String", "API_KEY", "\"${external.apiKey}\"")
            buildConfigField("int", "DATABASE_VERSION", "${app.database_version}")

            signingConfig signingConfigs.releaseKeystore

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFiles fileTree(dir: 'proguard', include: ['*.pro']).asList().toArray()
        }
    }

    buildFeatures {
        dataBinding true
    }
}

kapt {
    correctErrorTypes true // for hilt
}

dependencies {
    implementation project(':resources')

    implementation project(':common')
    implementation project(':model')
    implementation project(':api')
    implementation project(':domain')

    // features
    implementation project(':feature-home')
    implementation project(':feature-search')
    implementation project(':feature-setting')

    implementation deps.kotlin.stdlib

    // androidx
    implementation deps.androidx.core
    implementation deps.androidx.activity
    implementation deps.androidx.fragment
    implementation deps.androidx.recyclerview
    implementation deps.androidx.swiperefreshlayout
    implementation deps.androidx.appcompat
    implementation deps.androidx.material
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.lifecycle_viewmodel
    implementation deps.androidx.lifecycle_livedata
    implementation deps.androidx.lifecycle_runtime
    implementation deps.androidx.startup
    implementation deps.androidx.paging
    implementation deps.androidx.navigation_fragment
    implementation deps.androidx.navigation_ui
    implementation deps.androidx.datastore_prefs
    implementation deps.androidx.workmanager
    implementation deps.androidx.hilt_work

    // androidx.room
    implementation deps.androidx.room_runtime
    implementation deps.androidx.room_ktx
    kapt deps.androidx.room_compiler

    // dagger/hilt
    implementation deps.libraries.hilt
    kapt deps.libraries.hilt_kapt
    // hilt with androidx
    kapt deps.androidx.hilt_kapt

    // timber
    implementation deps.libraries.timber

    // glide
    implementation deps.libraries.glide
    kapt deps.libraries.glide_kapt

    // okhttp
    implementation deps.libraries.okhttp

    // flipper (don't use a external gradle file, because of build types)
    debugImplementation 'com.facebook.flipper:flipper:0.90.2'
    debugImplementation 'com.facebook.soloader:soloader:0.10.1'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.90.2'

    releaseImplementation 'com.facebook.flipper:flipper-noop:0.90.2'
}
